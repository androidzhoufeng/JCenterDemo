apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

def version_name = "1.0.3"

android {
    compileSdkVersion 28


    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 2
        versionName version_name

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:appcompat-v7:28.0.0'
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

//// This is the library version used when deploying the artifact
group = "com.github.zfeng"
version = version_name //PROJ_VERSION 替换成自己的的版本号

def siteUrl = 'https://github.com/zhoufengAndroid/JCenterDemo'
def gitUrl = 'https://github.com/zhoufengAndroid/JCenterDemo.git'

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                description "a jcenter demo library" //替换成自己的工程描述

                // Add your description here
                name "JCenterDemo"  //替换成自己的工程名称
                url siteUrl //替换成自己的工程网络地址，例如github中发布的源码地址

                // Set your license
                licenses {
                    license {
                        name "The Apache Software License, Version 2.0"
                        //替换证书类型，例如'The Apache Software License, Version 2.0'
                        url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        //替换证书地址，例如'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        distribution "..." //证书描述
                    }
                }
                developers {
                    developer {
                        id "zfeng" //替换成自己的id
                        name "zfeng" //替换成自己的名字
                        email "zf709099636@aliyun.com" //替换成自己的邮箱
                    }
                }
                scm {
                    connection gitUrl //替换成自己的Git仓库地址。
                    developerConnection gitUrl //替换成自己的Git仓库地址。
                    url siteUrl //替换成自己的项目主页。
                }

                dependencies {
                    dependency {
                        groupId "com.github.zfeng"
                        version version_name
                        artifactId "library"
                        scope "implementation"
                    }

                }
            }
        }
    }
}

task sourcesJar(type: Jar) { //这个task不需要修改，发布内容为jar类型
    classifier = 'sources'
}

task javadoc(type: Javadoc) { //这里不需要修改，发布的有javaDoc
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    failOnError = false
}

task javadocJar(type: Jar, dependsOn: javadoc) { //这里也不需要修改
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts { //这里不需要修改，发布内容包括javadocJar和sourcesJar
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

def bintray_user = properties.getProperty("bintray.user")
def bintray_key = properties.getProperty("bintray.apikey")
def bintray_gpg_pwd = properties.getProperty("bintray.gpg.password")

bintray {
    user = bintray_user
    key = bintray_key
    configurations = ['archives']

    pkg {
        repo = "zfeng"
        name = "library" //替换成自己的
        websiteUrl = siteUrl //替换成自己的
        vcsUrl = gitUrl  //替换成自己的
        licenses = ["Apache-2.0"]
//        userOrg = "zfeng" //组织名称，换成自己的
        publish = true //这个必须设置为true，默认为false只有这样才能发布
        version {
            desc = "....." //替换成自己的
            //gpg的配置是在为jcenter自动上传到mavenCenter做准备，没有本签名配置无法将包同步到mavenCenter,因为只有经过签名的包才允许上传到maveCenter.
            gpg {
                sign = true //Determines whether to GPG sign the files. The default is false
                passphrase = bintray_gpg_pwd
                //注意，这里是在生成mavenCenter公钥和密钥时候的密码，后面后解释
            }
        }
    }
}
